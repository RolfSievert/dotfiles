# Maintainer: Rolf Sievert
# AppImage PKGBUILD guide: https://wiki.archlinux.org/title/User:SergeyK/AppImage_package_guidelines
# MuseScore releases page: https://github.com/musescore/MuseScore/releases

_pkgname=musescore
_appname=MuseScore
pkgname="${_pkgname}"
pkgver=4.6.5
_buildid=253511702
_desktopname="org.${_pkgname}.${_appname}4portable.desktop"
pkgrel=1                      # increase when you need to rebuild without upstream changes
pkgdesc="MuseScore is an open source and free music notation software. For support, contribution, bug reports, visit MuseScore.org. Fork and make pull requests!"
arch=('x86_64')
url="https://github.com/musescore/MuseScore"
license=('GNU GENERAL PUBLIC LICENSE')
depends=('zlib' 'fuse2')
options=(!strip) # only build a single package
_appimage="${_appname}-Studio-${pkgver}.${_buildid}-x86_64.AppImage"
source=(
    "${url}/releases/download/v${pkgver}/${_appimage}"
)
noextract=("${_appimage}")
sha256sums=(
    '193daa0ea18bcfa90a47145a842275b8069b7b2b8d153e562b15fab5fe50fcaf'
)

prepare() {
    chmod +x "${_appimage}"
    ./"${_appimage}" --appimage-extract

    # Replace Exec with installed program name
    sed -i \
        's/^Exec=.*/Exec=musescore %U/' \
        "${srcdir}/squashfs-root/${_desktopname}"
}

package() {
    # AppImage
    install -Dm755 "${srcdir}/${_appimage}" "${pkgdir}/opt/${pkgname}/${pkgname}.AppImage"

    # Desktop file
    install -Dm644 \
        "${srcdir}/squashfs-root/${_desktopname}" \
        "${pkgdir}/usr/share/applications/${_desktopname}"

    # Icon images
    install -dm755 "${pkgdir}/usr/share/"
    cp -a "${srcdir}/squashfs-root/usr/share/icons" "${pkgdir}/usr/share/"

    # Symlink executable
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/${pkgname}.AppImage" "${pkgdir}/usr/bin/${_pkgname}"
}
